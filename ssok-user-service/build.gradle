plugins {
    alias(libs.plugins.springBoot)
    alias(libs.plugins.dependencyManagement)
    // protobuf 플러그인
    alias(libs.plugins.protobufPlugin)
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // Common 모듈 의존성
    implementation project(':ssok-common')

    // Spring boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // swagger
    implementation libs.swagger

    // Spring Cloud Dependencies
    implementation libs.springCloud

    // OpenFeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'


    // lombook
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // PostgreSQL Driver
    runtimeOnly 'org.postgresql:postgresql'

    // Logback
    implementation libs.logback

    // Redis
    implementation libs.redis

    // JWT
    implementation libs.jwtApi
    runtimeOnly libs.jwtImpl
    runtimeOnly libs.jwtJackson

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Kubernetes Config
    implementation 'org.springframework.cloud:spring-cloud-starter-kubernetes-client-config'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation "org.springframework.cloud:spring-cloud-starter-bootstrap"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    // H2(테스트용)
    runtimeOnly 'com.h2database:h2'

    // AWS S3
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.470'

    // gRPC 라이브러리
    implementation(libs.grpcStub)
    implementation(libs.grpcProtobuf)
    runtimeOnly(libs.grpcNetty)

    // 컴파일 에러 방지용 어노테이션 라이브러리
    compileOnly(libs.jakartaAnnotation)

    // protobuf 유틸(protobuf -> Json 변환 등)이 필요할 경우 (디버깅 용도)
    // implementation(libs.protobufJavaUtil)
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${libs.versions.springCloud.get()}")
    }
}

test {
    enabled = true
    useJUnitPlatform()
}

tasks.register("prepareKotlinBuildScriptModel"){}

// protobuf 설정
protobuf {
    protoc {
        // Protocol Buffers 컴파일러
        artifact = libs.protobufCompiler.get().toString()
    }
    plugins {
        grpc {
            // gRPC Java 코드 생성용 플러그인 (Stub, Service 등)
            artifact = libs.grpcCodegen.get().toString()
        }
    }
    generateProtoTasks {
        all().configureEach {
            plugins {
                grpc {}
            }
        }
    }
}

